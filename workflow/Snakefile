include: "rules/common.smk"


module dna_seq_varlociraptor:
    snakefile:
        github(
            "snakemake-workflows/dna-seq-varlociraptor",
            path="workflow/Snakefile",
            tag="v5.6.0",
        )
    config:
        config


use rule * from dna_seq_varlociraptor


report: "report/workflow.rst"


# Remove loh-events in case no normal sample is present
if "normal" not in dna_seq_varlociraptor.samples["alias"].unique():
    list_keys = list(
        dna_seq_varlociraptor.config["calling"]["fdr-control"]["events"].keys()
    )
    for k in list_keys:
        if k.startswith("loh"):
            dna_seq_varlociraptor.config["calling"]["fdr-control"]["events"].pop(k)
else:
    tissue_counts = dna_seq_varlociraptor.samples.groupby("alias").size()
    if tissue_counts["normal"] != tissue_counts["tumor"]:
        raise WorkflowError(
            "Invalid definition of tumor and normal samples in sample sheet. Only tumor/normal or tumor only groups are allowed at once."
        )

if not "ffpe" in dna_seq_varlociraptor.samples.columns:
    dna_seq_varlociraptor.samples["ffpe"] = 0
elif (
    dna_seq_varlociraptor.samples.loc[
        dna_seq_varlociraptor.samples["alias"] == "normal", "ffpe"
    ]
    == 1
).any():
    raise WorkflowError(
        "Invalid value for ffpe column in sample sheet. ffpe treated normal samples are not supported."
    )

for sample, events in zip(
    ["normal", "tumor"],
    ["germline,loh,loh_or_amplification", "somatic_tumor_low,somatic_tumor_high"],
):
    dna_seq_varlociraptor.samples.loc[
        dna_seq_varlociraptor.samples["alias"] == sample, "mutational_burden_events"
    ] = events
